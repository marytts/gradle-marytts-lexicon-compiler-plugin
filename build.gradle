plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'

gradlePlugin {
    plugins {
        lexiconCompilerPlugin {
            id = 'de.dfki.mary.lexicon-compiler'
            implementationClass = 'de.dfki.mary.lexicon.LexiconPlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: '5.2'
    compile group: 'nz.ac.waikato.cms.weka', name: 'weka-dev', version: '3.7.6'
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            force 'org.codehaus.groovy:groovy-all:2.4.7'
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/marytts/gradle-marytts-lexicon-compiler-plugin'
    vcsUrl = 'https://github.com/marytts/gradle-marytts-lexicon-compiler-plugin'
    description = 'Lexicon compiler plugin for MaryTTS (incubating)'
    tags = ['text-to-speech', 'synthesis']

    plugins {
        lexiconCompilerPlugin {
            id = 'de.dfki.mary.lexicon-compiler'
            displayName = 'Gradle MaryTTS lexicon compiler plugin'
        }
    }
}
