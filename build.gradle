plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'groovy'
    id 'signing'
}

group 'de.dfki.mary'
version '0.4.0-SNAPSHOT'
description 'Lexicon compiler plugin for MaryTTS'
ext {
    url = 'https://github.com/marytts/gradle-marytts-lexicon-compiler-plugin'
    isReleaseVersion = !version.endsWith('-SNAPSHOT')
}

pluginBundle {
    website = url
    vcsUrl = url
    description = 'Lexicon compiler plugin for MaryTTS (incubating)'
    tags = ['text-to-speech', 'synthesis']
}

gradlePlugin {
    plugins {
        lexiconCompilerPlugin {
            id = 'de.dfki.mary.lexicon-compiler'
            implementationClass = 'de.dfki.mary.lexicon.LexiconPlugin'
            displayName = 'Gradle MaryTTS lexicon compiler plugin'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                url 'https://mlt.jfrog.io/artifactory/mlt-mvn-releases-local'
            }
        }
        filter {
            includeGroup 'de.dfki.lt.jtok'
        }
    }
}

dependencies {
    implementation group: 'de.dfki.mary', name: 'marytts-runtime', version: '5.2.1', {
        exclude group: 'com.twmacinta', module: 'fast-md5'
        exclude group: 'gov.nist.math', module: 'Jampack'
    }
    implementation group: 'nz.ac.waikato.cms.weka', name: 'weka-dev', version: '3.7.6'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
}

test {
    useTestNG()
    systemProperty 'java.io.tmpdir', temporaryDir
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    publications {
        withType(MavenPublication).configureEach {
            pom {
                name = project.name
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        id = 'psibre'
                        name = 'Ingmar Steiner'
                    }
                }
                scm {
                    connection = 'scm:git:git//github.com:marytts/gradle-marytts-lexicon-compiler-plugin.git'
                    developerConnection = 'scm:git:ssh://github.com:marytts/gradle-marytts-lexicon-compiler-plugin.git'
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
